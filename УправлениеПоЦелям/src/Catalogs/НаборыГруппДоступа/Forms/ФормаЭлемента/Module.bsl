#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ТолькоПросмотр = Истина;
	
	СписокВыбора = Элементы.ТипЭлементовНабора.СписокВыбора;
	ДобавитьЭлементСписка(СписокВыбора, "ГруппыДоступа");
	ДобавитьЭлементСписка(СписокВыбора, "ГруппыПользователей");
	ДобавитьЭлементСписка(СписокВыбора, "Пользователи");
	ДобавитьЭлементСписка(СписокВыбора, "ГруппыВнешнихПользователей");
	ДобавитьЭлементСписка(СписокВыбора, "ВнешниеПользователи");
	
	УстановитьСтраницуРеквизитовПоТипу(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипЭлементовНабораПриИзменении(Элемент)
	
	УстановитьСтраницуРеквизитовПоТипу(ЭтотОбъект);
	Объект.Группы.Очистить();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВключитьВозможностьРедактирования(Команда)
	
	ТолькоПросмотр = Ложь;
	
	ПоказатьПредупреждение(,
		НСтр("ru = 'Набор групп доступа не следует изменять, так как он сопоставлен с разными ключами доступа.
		           |Чтобы исправить нестандартную проблему следует удалить набор групп доступа или
		           |связь с ним в регистрах и выполнить процедуру обновления доступа.'"));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ДобавитьЭлементСписка(СписокВыбора, ИмяСправочника)
	
	ПустойИдентификатор = Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000");
	
	СписокВыбора.Добавить(Справочники[ИмяСправочника].ПолучитьСсылку(ПустойИдентификатор),
		Метаданные.Справочники[ИмяСправочника].Представление());
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСтраницуРеквизитовПоТипу(Форма)
	
	Если ТипЗнч(Форма.Объект.ТипЭлементовНабора) = Тип("СправочникСсылка.Пользователи")
	 Или ТипЗнч(Форма.Объект.ТипЭлементовНабора) = Тип("СправочникСсылка.ВнешниеПользователи") Тогда
		
		Форма.Элементы.РеквизитыНаборов.ТекущаяСтраница = Форма.Элементы.РеквизитыНабораИзОдногоПользователя;
	Иначе
		Форма.Элементы.РеквизитыНаборов.ТекущаяСтраница = Форма.Элементы.РеквизитыНабораГрупп;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
